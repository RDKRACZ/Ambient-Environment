buildscript {
    repositories {
        maven {
            name = "BlameJared"
            url = 'https://maven.blamejared.com'
        }
        mavenCentral()
    }
//    dependencies {
//        classpath group: 'com.blamejared', name: 'ModTemplate', version: '1.+', changing: true
//    }
}

plugins {
    id "fabric-loom" version "0.8-SNAPSHOT"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}
apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/secrets.gradle'
//apply plugin: 'com.blamejared.modtemplate'

if (project.hasProperty('secretFile')) {
    loadSecrets(new File((String) findProperty('secretFile')))
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

version = 'fabric-1.17.1-6.0.0' + ".${getBuildNumber()}"
group = 'com.blamejared.ambientenvironment'
archivesBaseName = 'AmbientEnvironment'

//modTemplate {
//    mcVersion "1.17"
//    curseHomepage "https://www.curseforge.com/minecraft/mc-mods/ambient-environment"
//    displayName "AmbientEnvironment Fabric"
//
//    changelog {
//        enabled true
//        firstCommit "d45e9b2ec81cd76424d53e164a9bd9f1293ff554"
//        repo "https://github.com/jaredlll08/Ambient-Environment"
//    }
//    versionTracker {
//        enabled false
//    }
//    webhook {
//        enabled true
//        curseId "320163"
//        avatarUrl "https://media.forgecdn.net/avatars/201/25/636917476563554470.png"
//    }
//}

dependencies {
    minecraft "com.mojang:minecraft:1.17.12"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.11.3"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.34.9+1.17"
}

processResources {

    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {

        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {

    it.options.encoding = 'UTF-8'
    it.options.release = 16
}

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }
    repositories {
        maven {
            // Sets the maven URL
            url "file://" + System.getenv("local_maven")
        }
    }
}


curseforge {

    apiKey = findProperty('curseforge_api_token') ?: 0
    def versions = ["1.17.1", "1.17", "1.17-Snapshot"]

    project {
        id = "320163"
        releaseType = 'release'
        changelog = "Port to Fabric"
        changelogType = 'markdown'
        addGameVersion "Fabric"

        versions.each {

            addGameVersion "${it}"
        }

        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false;
    }
}

String getBuildNumber() {

    return System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0';
}
